
model {
  # Priors
  beta ~ dnorm(50, 0.0001)
  sigma_s ~ dt(0, 1/6.25, 3)T(0,) 
  for (j in 1:D) {
    sigma_d[j] ~ dt(0, 1/6.25, 3)T(0,) # (hetero) SD for random effects for days
  }  
  for (k in 1:M) {
    sigma_epsilon[k] ~ dt(0, 1/6.25, 3)T(0,) #SD for error
  } 


  # transformation
  prec_s <- 1 / sigma_s^2
  for (j in 1:D) {
    prec_d[j] <- 1 / sigma_d[j]^2
  } 
  for (k in 1:M) {
    prec_epsilon[k] <- 1 / sigma_epsilon[k]^2
  }

  # Level 3
  for (i in 1:N) {
    s[i] ~ dnorm(0, prec_s)
  }

  # Level 2
  for (i in 1:N) {
    for (j in 1:D) {
      d[i, j] ~ dnorm(0, prec_d[j])
    }
  }

  # Level 1
  # Likelihood
  for (i in 1:N) {
    for (j in 1:D) {
      for (k in 1:M) {
        mu[i, j, k] <- beta + s[i] + d[i, j]
        y[i, j, k] ~ dnorm(mu[i, j, k], prec_epsilon[k])
      }
    }
  }
}

