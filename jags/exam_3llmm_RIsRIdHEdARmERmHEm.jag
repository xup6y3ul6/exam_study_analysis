
model {
  # Priors
  beta ~ dnorm(50, 0.0001)
  sigma_s ~ dt(0, 1/6.25, 3)T(0,) 
  for (j in 1:D) {
    sigma_d[j] ~ dt(0, 1/6.25, 3)T(0,) # (hetero) SD for random effects for days
  }  
  for (k in 1:M) {
    sigma_epsilon[k] ~ dt(0, 1/6.25, 3)T(0,) #SD for error
  } 
  eta_m ~ dt(0, 1/6.25, 3)T(0,)  
  phi_m ~ dnorm(0, 4)T(-1, 1)  


  # transformation
  prec_s <- 1 / sigma_s^2
  for (j in 1:D) {
    prec_d[j] <- 1 / sigma_d[j]^2
  } 
  for (k in 1:M) {
    prec_epsilon[k] <- 1 / sigma_epsilon[k]^2
  }
  prec_eta_m <- 1 / eta_m^2
  tau2_m <- eta_m^2 / (1 - phi_m^2)


  # Likelihood
  for (i in 1:N) {
    ## Level 3:
    s[i] ~ dnorm(0, prec_s)

    for (j in 1:D) {
      ## Level 2:
      d[i, j] ~ dnorm(0, prec_d[j])

      ## Level 1: 
      omega[i, j, 1] ~ dnorm(0, prec_eta_m) # AR(1) process for the first moment 
      for (k in 2:M) {
        omega[i, j, k] ~ dnorm(phi_m * omega[i, j, k - 1], prec_eta_m)
      }
      for (k in 1:M) {
        mu[i, j, k] <- beta + s[i] + d[i, j] + omega[i, j, k]
        y[i, j, k] ~ dnorm(mu[i, j, k], prec_epsilon[k])
      }
    }
  }
}

